Scriptname SkyrimNet_SexLab_Scene_Manager extends Quest 

Import SkyrimNet_SexLab_Utilities

SkyrimNet_SexLab_Main Property main Auto
SkyrimNet_SexLab_AnimDb Property animdb Auto

SexLabFramework Property sexlab Auto
sslThreadSlots Property threadSlots Auto
sslActorLibrary Property actorLib Auto

Faction Property OStimActorCountFaction = None Auto

; sl_scene_generic is returned when there are no more sl_scenes available
; If a sl_scene is not found, sl_scene_generic is returned
; to make sure a description is always possible
SkyrimNet_SexLab_Scene Property sl_scene_generic = None Auto
SkyrimNet_SexLab_Scene[] Property sl_scenes Auto

; We use Form so we can use CreateFormArray if we need to increase the size 
Form[] thread_scene
SkyrimNet_SexLab_Scene_Creator[] Property creators Auto

; -------------------------------------
Faction Property SkyrimNet_SexLab_Faction_Victim Auto

; Threads filename 
String threads_filename = "Data/SKSE/Plugins/SkyrimNet_SexLab/threads.json"

; -------------------------------------
; Thread Count
; -------------------------------------
int thread_counter = 0 

; -------------------------------------
; Group Info Object 
; -------------------------------------
int Property group_info = 0 Auto

; ---------------------------------------
; Location of the Scenes 
; ---------------------------------------
String SCENES_FOLDER = "Data/SKSE/Plugins/SkyrimNet_SexLab/scenes/"

; ---------------------------------------
; speaker_last is used when save is called 
; ---------------------------------------
Actor speaker_last = None

; --------------------------------------------
; Since returning a None array cause an error
; we set the empty
; --------------------------------------------
sslBaseAnimation[] Property empty = None Auto
sslBaseAnimation[] Property cancel = None Auto
sslBaseAnimation[] Property ui_pending = None Auto

Function Trace(String func, String msg="", Bool notification=False)
    String logged = SkyrimNet_SexLab_WebUI.TraceLog("SkyrimNet_SexLab_Scene_Manager", func, msg)
    if notification
        Debug.Notification(msg)
    endif 
EndFunction

Function Setup() 
    Bool links_ok = Setup_CheckLinks()
    if !links_ok
        return
    endif

    Trace("Setup","")
    ; Auto fills bake into saves. Renaming scenes→sl_scenes / scene_generic→
    ; sl_scene_generic leaves the new names empty on old saves while creators
    ; (unchanged) still work — rebuild every Setup from known FormIDs.
    if !RebuildScenePool()
        Trace("Setup","RebuildScenePool failed, aborting", true)
        return
    endif

    ; Used to check if an actor is controled by OStim
    if Game.GetModByName("Ostim.esp") != 255
        OStimActorCountFaction = Game.GetFormFromFile(0xECA, "Ostim.esp") as Faction
        Trace("Setup","Found Ostim.esp, OStimActorCountFaction set to "+OStimActorCountFaction)
    else 
        OStimActorCountFaction = None 
    endif 

    if !cancel 
        cancel = new sslBaseAnimation[1]
        cancel[0] = None 
    endif 
    if !empty 
        empty = new sslBaseAnimation[2]
        empty[0] = None 
        empty[1] = None 
    endif 
    if !ui_pending 
        ui_pending = new sslBaseAnimation[1]
        ui_pending[0] = None 
    endif 
    bool empty_equals_cancel = empty == cancel
    Trace("Initialize", "empty == cancel: "+empty_equals_cancel)

    ; is_generic=true permanently marks the fallback Scene when the pool is exhausted
    sl_scene_generic.Initialize(-1, self, true) 

    int i = sl_scenes.length - 1
    while 0 <= i 
        if sl_scenes[i] == None
            Trace("Setup","sl_scenes["+i+"] is None, aborting", true)
            return
        endif
        sl_scenes[i].Initialize(i, self, false)
        i -= 1 
    endwhile  

    i = creators.length - 1 
    while 0 <= i 
        if creators[i] == None
            Trace("Setup","creators["+i+"] is None, aborting", true)
            return
        endif
        creators[i].Initialize(i, self) 
        i -= 1 
    endwhile 

    if !thread_scene
        Trace("Setup","creating thread_scene map")
        thread_scene = new form[32]
    endif 

    ; Reload the group_tags in case they where changed each time.
    if group_info == 0
        group_info = JValue.readFromFile("Data/SKSE/Plugins/SkyrimNet_Sexlab/group_tags.json")
        JValue.retain(group_info)
    else
        int group_info_new = JValue.readFromFile("Data/SKSE/Plugins/SkyrimNet_Sexlab/group_tags.json")
        JValue.releaseAndRetain(group_info, group_info_new)
        group_info = group_info_new
    endif
    RegisterEventsActions()
    RegisterEventsSexLab()

    ; SexLab always starts with no active threads; clear stale file from prior session
    GetThreadsJson()
EndFunction 

Bool Function Setup_CheckLinks()
    Bool links_ok = true

    if main == None
        links_ok = false
    endif

    animdb = (self as Quest) as SkyrimNet_SexLab_AnimDb
    if animdb == None
        links_ok = false
    endif

    if SexLab == None
        links_ok = false
    endif

    if threadSlots == None
        links_ok = false
    endif

    if actorLib == None
        links_ok = false
    endif

    return links_ok
EndFunction

; --------------------------------------------------------------------
; Create Creator 
; --------------------------------------------------------------------
SkyrimNet_SexLab_Scene_Creator Function CreateCreator(String intent, Actor[] actors, Actor speaker, Actor target, String tags="", String setting_name="")
    Trace("CreateCreator","intent: "+intent+" actors: "+JoinActorsToJson(actors)+" speaker: "+GetDisplayName(speaker)+" target: "+GetDisplayName(target)+" tags: "+tags+" setting_name: "+setting_name)
    int i = 0
    int num_creators = creators.length 
    while i < num_creators
        if creators[i].TryClaim()
            if creators[i].Setup(intent, actors, speaker, target, tags, setting_name)
                return creators[i]
            endif
            creators[i].Release()
            Trace("CreateCreator", "Setup failed for creators["+i+"], trying next slot")
        endif 
        i += 1 
    endwhile
    Trace("CreateCreator", "no inactive creator available (or all Setup failed)")
    return None
EndFunction

; --------------------------------------------------------------------
; Get Scene 
; --------------------------------------------------------------------
SkyrimNet_SexLab_Scene Function CreateSceneByCreator(SkyrimNet_SexLab_Scene_Creator creator, sslThreadController thread) 
    if creator == None || thread == None
        Trace("CreateSceneByCreator", "creator or thread is None, aborting")
        return None
    endif
    SkyrimNet_SexLab_Scene sl_scene = GetSceneInactive(thread)
    if sl_scene == None
        Trace("CreateSceneByCreator", "GetSceneInactive returned None, aborting")
        return None
    endif
    if !sl_scene.Setup(creator)
        Trace("CreateSceneByCreator", "Setup failed, releasing scene")
        sl_scene.Release()
        return None
    endif
    return sl_scene 
EndFunction 

SkyrimNet_SexLab_Scene Function CreateSceneWithoutCreator(sslThreadController thread) 
    if thread == None
        Trace("CreateSceneWithoutCreator", "thread is None, aborting")
        return None
    endif
    SkyrimNet_SexLab_Scene_Creator creator = CreateCreator("", thread.Positions, None, None, "", "")
    if creator == None
        Trace("CreateSceneWithoutCreator", "CreateCreator returned None, aborting")
        return None
    endif
    SkyrimNet_SexLab_Scene sl_scene = CreateSceneByCreator(creator, thread)
    ; Setup copied all values out of the creator; free the pool slot so it is not leaked.
    creator.Release()
    return sl_scene
EndFunction 

; --------------------------------------
; These will get a sl_scene if they can find it or return sl_scene_generic 
; create_if_missing=False: read-only lookup (no allocate/Setup/Release) for JSON/decorators
; --------------------------------------
SkyrimNet_SexLab_Scene Function GetSceneByThread(sslThreadController thread, Bool any_state=False, Bool create_if_missing=True)
    if !any_state
        String s = (thread as sslThreadModel).GetState()
        if s != "animating" && s != "prepare"
            return None 
        endif 
    endif 

    int tid = thread.tid
    if tid < thread_scene.length && thread_scene[tid] != None 
        SkyrimNet_SexLab_Scene sl_scene = thread_scene[tid] as SkyrimNet_SexLab_Scene
        ; thread_scene[tid] is authoritative during Setup (status still INACTIVE until
        ; Setup ends). Keep/rebind on tid match or bound None; never Release solely for
        ; INACTIVE or transient GetThread() == None. Release only on tid mismatch.
        if sl_scene != None
            sslThreadController bound = sl_scene.GetThread()
            if bound == None || bound.tid == thread.tid
                if bound != thread
                    sl_scene.SetThread(thread)
                endif
                return sl_scene
            endif
            Trace("GetSceneByThread", "stale thread_scene["+tid+"] bound to tid:"+bound.tid+", releasing sid:"+sl_scene.sid)
            if !create_if_missing
                return None
            endif
            thread_scene[tid] = None
            sl_scene.Release()
        endif
    endif 

    if !create_if_missing
        return None
    endif

    SkyrimNet_SexLab_Scene_Creator creator = CreateCreator("", thread.Positions, None, None, "", "")
    if creator == None
        Trace("GetSceneByThread", "CreateCreator returned None, aborting")
        return None
    endif
    SkyrimNet_SexLab_Scene sl_scene = CreateSceneByCreator(creator, thread)
    ; Setup copied all values out of the creator; free the pool slot so it is not leaked.
    creator.Release()
    if sl_scene == None 
        Trace("GetSceneByThread", "CreateSceneByCreator returned None, aborting")
        return None
    endif
    return sl_scene
EndFunction


SkyrimNet_SexLab_Scene Function GetSceneByThreadId(int tid, bool any_state=False, Bool create_if_missing=True)
    if sexlab == None 
        Trace("GetSceneBythreadId","Sexlab is None, aborting")
        return None
    endif 
    sslThreadController thread = SexLab.GetController(tid)
    if thread == None 
        Trace("GetSceneBythreadId", "thread is None, aborting")
        return None 
    endif 
    return GetSceneByThread(thread, any_state, create_if_missing) 
EndFunction 

; Bind or create a scene for an external / DOM-started SexLab thread (HookAnimationStart path).
SkyrimNet_SexLab_Scene Function EnsureSceneForThread(sslThreadController thread)
    return GetSceneByThread(thread, any_state=False, create_if_missing=True)
EndFunction

; ----------------------------------------
; Rebuild pool/creator refs from plugin FormIDs. Required after property renames
; (scenes→sl_scenes, scene_generic→sl_scene_generic): Auto fills bake into saves,
; so old saves keep empty new-name properties while creators still work.
bool Function RebuildScenePool()
    String plugin = "SkyrimNet_SexLab.esp"
    ; Scene_00..09 local IDs (matches ESP / Spriggit fill order)
    int[] scene_ids = new int[10]
    scene_ids[0] = 0x80C
    scene_ids[1] = 0x802
    scene_ids[2] = 0x809
    scene_ids[3] = 0x80A
    scene_ids[4] = 0x80B
    scene_ids[5] = 0x80D
    scene_ids[6] = 0x80E
    scene_ids[7] = 0x80F
    scene_ids[8] = 0x810
    scene_ids[9] = 0x811

    Trace("RebuildScenePool", "rebuilding sl_scenes from GetFormFromFile")
    sl_scenes = new SkyrimNet_SexLab_Scene[10]
    int i = 0
    while i < 10
        sl_scenes[i] = Game.GetFormFromFile(scene_ids[i], plugin) as SkyrimNet_SexLab_Scene
        if sl_scenes[i] == None
            Trace("RebuildScenePool", "scene FormID "+scene_ids[i]+" is None", true)
            return false
        endif
        i += 1
    endwhile

    Trace("RebuildScenePool", "rebuilding sl_scene_generic from GetFormFromFile")
    sl_scene_generic = Game.GetFormFromFile(0x812, plugin) as SkyrimNet_SexLab_Scene
    if sl_scene_generic == None
        Trace("RebuildScenePool", "sl_scene_generic FormID 0x812 is None", true)
        return false
    endif

    bool need_creators = !creators || creators.length != 10
    if !need_creators
        i = 0
        while i < 10 && !need_creators
            if creators[i] == None
                need_creators = true
            endif
            i += 1
        endwhile
    endif
    if need_creators
        Trace("RebuildScenePool", "rebuilding creators from GetFormFromFile")
        creators = new SkyrimNet_SexLab_Scene_Creator[10]
        i = 0
        while i < 10
            creators[i] = Game.GetFormFromFile(0x813 + i, plugin) as SkyrimNet_SexLab_Scene_Creator
            if creators[i] == None
                Trace("RebuildScenePool", "creator FormID "+(0x813 + i)+" is None", true)
                return false
            endif
            i += 1
        endwhile
    endif
    return true
EndFunction

; Resolve sl_scene_generic from the property, or FormID 0x812 if still missing.
bool Function ResolveSceneGeneric()
    if sl_scene_generic != None
        return true
    endif
    sl_scene_generic = Game.GetFormFromFile(0x812, "SkyrimNet_SexLab.esp") as SkyrimNet_SexLab_Scene
    if sl_scene_generic == None
        Trace("ResolveSceneGeneric", "GetFormFromFile(0x812) returned None")
        return false
    endif
    Trace("ResolveSceneGeneric", "recovered sl_scene_generic via GetFormFromFile")
    return true
EndFunction

; Claim an inactive pool scene, reclaiming orphans (IsActive but no live thread).
; Falls back to sl_scene_generic; returns None only if generic cannot be resolved.
SkyrimNet_SexLab_Scene Function GetSceneInactive(sslThreadController thread) 
    if thread == None
        Trace("GetSceneInactive", "thread is None, aborting")
        return None
    endif
    ; Old saves may still have empty sl_scenes if Setup has not rebuilt yet this session.
    if !sl_scenes || sl_scenes.length == 0
        Trace("GetSceneInactive", "sl_scenes empty — RebuildScenePool")
        if !RebuildScenePool()
            Trace("GetSceneInactive", "RebuildScenePool failed", true)
            return None
        endif
        int j = 0
        while j < sl_scenes.length
            sl_scenes[j].Initialize(j, self, false)
            j += 1
        endwhile
        if sl_scene_generic != None
            sl_scene_generic.Initialize(-1, self, true)
        endif
    endif
    int i = 0 
    int num_scenes = sl_scenes.length 
    while i < num_scenes
        SkyrimNet_SexLab_Scene candidate = sl_scenes[i]
        if candidate != None
            ; Busy only if a live SexLab thread is attached — status alone is not enough
            ; when saves leave all slots ACTIVE with no animations running.
            if candidate.GetThreadActive()
                i += 1
            else
                if candidate.IsActive()
                    Trace("GetSceneInactive", "reclaiming orphan sl_scenes["+i+"] sid:"+candidate.sid)
                    candidate.Release()
                endif
                EnsureThreadSceneLargeEnough(thread.tid) 
                candidate.SetThread(thread)
                thread_scene[thread.tid] = candidate
                return candidate
            endif
        else
            i += 1 
        endif
    endwhile
    Trace("GetSceneInactive","Failed to find inactive sl_scene using generic")
    if !ResolveSceneGeneric()
        Trace("GetSceneInactive", "sl_scene_generic is None, aborting")
        return None
    endif
    ; Single shared fallback: do not rebind while it already serves a live thread (no CK pool expand).
    if sl_scene_generic.GetThreadActive()
        Trace("GetSceneInactive", "sl_scene_generic already active for another thread, refusing allocate")
        return None
    endif
    EnsureThreadSceneLargeEnough(thread.tid)
    sl_scene_generic.SetThread(thread) 
    thread_scene[thread.tid] = sl_scene_generic
    return sl_scene_generic
EndFunction 

SkyrimNet_SexLab_Scene Function GetSceneByActor(Actor akActor) 
    if akActor == None 
        Trace("GetSceneByActor","akActor is None, aborting")
        return None 
    endif 
    sslThreadController thread = GetThreadByActor(akActor) 
    if thread != None
        return GetSceneByThread(thread)
    endif
    SkyrimNet_SexLab_Scene sl_scene = FindSceneByActorInThreadScene(akActor)
    if sl_scene == None
        Trace("GetSceneByActor", "--- no scene for "+GetDisplayName(akActor)+" (thread not animating/prepare)")
    endif
    return sl_scene
EndFunction

sslThreadController Function GetThreadByActor(Actor akActor, bool any_state=False) 
    Trace("GetThread","actor:"+akActor.GetDisplayName())
    sslThreadController[] threads = ThreadSlots.Threads
    if threads.length == -1 
        return None 
    endif 

    int i = threads.length - 1
    while 0 <= i
        String status = (threads[i] as sslThreadModel).GetState()
        if any_state || status == "animating" || status == "prepare"
            Actor[] actors = threads[i].Positions
            int j = actors.length - 1
            while 0 <= j 
                if actors[j] == akActor
                    return threads[i]
                endif 
                j -= 1
            endwhile 
        endif 
        i -= 1
    endwhile
    return None 
EndFunction

; StageEnd can leave the SexLab controller outside animating/prepare while thread_scene is still bound.
SkyrimNet_SexLab_Scene Function FindSceneByActorInThreadScene(Actor akActor)
    if !thread_scene || akActor == None
        return None
    endif
    int i = 0
    int n = thread_scene.length
    while i < n
        SkyrimNet_SexLab_Scene sl_scene = thread_scene[i] as SkyrimNet_SexLab_Scene
        if sl_scene != None
            sslThreadController bound = sl_scene.GetThread()
            if bound != None
                Actor[] actors = bound.Positions
                if actors
                    int j = 0
                    while j < actors.length
                        if actors[j] == akActor
                            Trace("FindSceneByActorInThreadScene", "--- "+GetDisplayName(akActor)+" sid:"+sl_scene.sid+" tid:"+bound.tid)
                            return sl_scene
                        endif
                        j += 1
                    endwhile
                endif
            endif
        endif
        i += 1
    endwhile
    if sl_scenes
        i = 0
        n = sl_scenes.length
        while i < n
            SkyrimNet_SexLab_Scene sl_scene = sl_scenes[i]
            if sl_scene != None
                sslThreadController bound = sl_scene.GetThread()
                if bound != None
                    Actor[] actors = bound.Positions
                    if actors
                        int j = 0
                        while j < actors.length
                            if actors[j] == akActor
                                Trace("FindSceneByActorInThreadScene", "--- sl_scenes "+GetDisplayName(akActor)+" sid:"+sl_scene.sid+" tid:"+bound.tid)
                                return sl_scene
                            endif
                            j += 1
                        endwhile
                    endif
                endif
            endif
            i += 1
        endwhile
    endif
    return None
EndFunction 

;----------------------------------------------------------------------------------------------------
; Thread_Scene Functions 
;----------------------------------------------------------------------------------------------------
Function UnsetThread_Scene(int tid)
    if 0 <= tid && tid < thread_scene.length
        thread_scene[tid] = None 
    endif 
EndFunction

Function EnsureThreadSceneLargeEnough(int tid)
    if tid >= thread_scene.length
        Trace("EnsureThreadSceneLargeEnough","tid:"+tid+" thread_scene.length:"+thread_scene.length)
        int new_size = tid + 10
        Form[] resized = Utility.CreateFormArray(new_size)
        int i = 0
        int num_threads = thread_scene.length
        while i < num_threads 
            resized[i] = thread_scene[i]
            i += 1
        endwhile
        thread_scene = resized
    endif
EndFunction

;----------------------------------------------------------------------------------------------------
; Get SceneSettings
;----------------------------------------------------------------------------------------------------
String Function GetSceneSettingFilename(String setting_name)
    return SCENES_FOLDER+"/"+setting_name+".json"
EndFunction
String[] function GetSceneSettings()
    ; 1. Read all filenames from the directory that end in .json
    String[] files = MiscUtil.FilesInfolder(SCENES_FOLDER)

    
    ; Safety check: Handle empty directory or invalid paths smoothly
    if !files || files.Length == 0
        return Utility.CreateStringArray(0)
    endif
    
    ; 2. Initialize your setting_names array dynamically matching the file count
    ; (Vanilla Papyrus requires compile-time constants for array sizes, SKSE bypasses this)
    String[] setting_names = Utility.CreateStringArray(files.Length)
    
    ; 3. Loop through files, strip the extension, and populate setting_names
    int i = 0
    while i < files.Length
        String currentFile = files[i]
        
        ; Find the starting character index of the ".json" extension
        int extIndex = StringUtil.Find(currentFile, ".json")
        
        if extIndex != -1
            ; Extract everything from the start (index 0) up to the dot
            setting_names[i] = StringUtil.Substring(currentFile, 0, extIndex)
        else
            ; Fallback case if a filename slips through without an extension
            setting_names[i] = currentFile
        endif
        
        i += 1
    endwhile
    
    ; 4. Return the clean array of setting names
    return setting_names
endFunction

SkyrimNet_SexLab_Scene_Creator Function GetCreatorBySid(int creator_sid)
    int i = 0
    while i < creators.length
        if creators[i] && creators[i].IsActive() && creators[i].sid == creator_sid
            return creators[i]
        endif
        i += 1
    endwhile
    return None
EndFunction

SkyrimNet_SexLab_Scene Function GetSceneBySid(int scene_sid)
    if scene_sid >= 0 && scene_sid < sl_scenes.length
        return sl_scenes[scene_sid]
    endif
    return None
EndFunction

Function WebUI_OnYesNoResult(int creator_sid, int button)
    SkyrimNet_SexLab_Scene_Creator creator = GetCreatorBySid(creator_sid)
    if creator
        creator.ContinueAfterYesNo(button)
    else
        Trace("WebUI_OnYesNoResult", "no active creator for sid:"+creator_sid, true)
    endif
EndFunction

Function WebUI_OnSceneCreatorResult(int creator_sid, String json)
    SkyrimNet_SexLab_Scene_Creator creator = GetCreatorBySid(creator_sid)
    if creator
        Trace("WebUI_OnSceneCreatorResult", "sid:"+creator_sid+" creator found")
        creator.ContinueAfterSceneCreator(json)
    else
        ; Control Panel Scene Menu "new" (and other provisional sessions) have
        ; no pooled creator; sid 0 is also a valid pool slot so C++ cannot tell.
        Trace("WebUI_OnSceneCreatorResult", "no active creator for sid:"+creator_sid+" - handoff")
        WebUI_OnSceneCreatorHandoff(json)
    endif
EndFunction

; No pooled creator yet (TargetMenu Custom or Control Panel Scene Menu new): build one from JSON and finish.
Function WebUI_OnSceneCreatorHandoff(String json)
    Trace("WebUI_OnSceneCreatorHandoff", "")
    int obj = JValue.objectFromPrototype(json)
    if obj == 0
        Trace("WebUI_OnSceneCreatorHandoff", "bad json", true)
        return
    endif
    String ui_action = JMap.getStr(obj, "_action", "cancel")
    if ui_action != "start"
        JValue.release(obj)
        Trace("WebUI_OnSceneCreatorHandoff", "cancel/ignored action:"+ui_action)
        return
    endif

    int pos_arr = JMap.getObj(obj, "_positions")
    int count = JArray.count(pos_arr)
    if count < 1
        JValue.release(obj)
        Trace("WebUI_OnSceneCreatorHandoff", "no positions", true)
        return
    endif

    Actor[] actors = PapyrusUtil.ActorArray(count)
    int i = 0
    int valid = 0
    while i < count
        int po = JArray.getObj(pos_arr, i)
        Actor ak = None
        if po > 0
            int form_id = JMap.getInt(po, "_form_id", 0)
            if form_id != 0
                ak = Game.GetFormEx(form_id) as Actor
            endif
        endif
        if ak != None
            actors[valid] = ak
            valid += 1
        else
            Trace("WebUI_OnSceneCreatorHandoff", "missing actor at index:"+i+" form_id:"+JMap.getInt(po, "_form_id", 0), true)
        endif
        i += 1
    endwhile
    if valid < 1
        JValue.release(obj)
        Trace("WebUI_OnSceneCreatorHandoff", "no valid actors", true)
        return
    endif
    if valid != count
        Actor[] trimmed = PapyrusUtil.ActorArray(valid)
        i = 0
        while i < valid
            trimmed[i] = actors[i]
            i += 1
        endwhile
        actors = trimmed
    endif

    String intent = JMap.getStr(obj, "_intent", "sexual activities")
    String tags = JMap.getStr(obj, "_start_tags", "")
    if tags == ""
        tags = JMap.getStr(obj, "_method", "")
    endif
    if tags == ""
        String tags_str = JMap.getStr(obj, "_tags", "")
        if tags_str != ""
            tags = tags_str
        endif
    endif
    String style = JMap.getStr(obj, "_style", "normally")
    Actor speaker = actors[0]
    Actor target = None
    if actors.length >= 2
        target = actors[1]
    endif

    SkyrimNet_SexLab_Scene_Creator creator = CreateCreator(intent, actors, speaker, target, tags, "")
    if creator == None
        JValue.release(obj)
        Trace("WebUI_OnSceneCreatorHandoff", "CreateCreator returned None", true)
        return
    endif

    ; Scene Creator already shown via C++ TargetMenu path.
    creator.scene_creator_menu_called = true

    if style != ""
        creator.SetStyle(style)
    endif

    if !creator.LockAllActorLock()
        JValue.release(obj)
        creator.Release()
        Trace("WebUI_OnSceneCreatorHandoff", "LockAllActorLock failed", true)
        return
    endif

    creator.ApplyWebUIState(obj)
    JValue.release(obj)

    sslBaseAnimation[] animations = creator.ResolveAnimationsFromUI()
    SkyrimNet_SexLab_Scene sl_scene = creator.FinishStartScene(animations)
    if sl_scene == None
        Trace("WebUI_OnSceneCreatorHandoff", "FinishStartScene returned None", true)
    endif
EndFunction

Function WebUI_OnSceneCreatorLoad(int creator_sid, String setting_name)
    SkyrimNet_SexLab_Scene_Creator creator = GetCreatorBySid(creator_sid)
    if creator
        creator.LoadPresetFromWebUI(setting_name)
    endif
EndFunction

Bool Function SceneSettingNameIsValid(String setting_name)
    if setting_name == "" || setting_name == "none"
        return False
    endif
    if StringUtil.Find(setting_name, "/") != -1
        return False
    endif
    if StringUtil.Find(setting_name, "\\") != -1
        return False
    endif
    if StringUtil.Find(setting_name, "..") != -1
        return False
    endif
    return True
EndFunction

Function SaveSceneSettingFromWebUIJson(String json)
    int obj = JValue.objectFromPrototype(json)
    if obj == 0
        Trace("SaveSceneSettingFromWebUIJson", "bad json", True)
        return
    endif
    String setting_name = JMap.getStr(obj, "_scene_preset", "")
    if !SceneSettingNameIsValid(setting_name)
        Trace("SaveSceneSettingFromWebUIJson", "invalid name:"+setting_name, True)
        JValue.release(obj)
        return
    endif
    int setting_id = JMap.object()
    String style = JMap.getStr(obj, "_style", "")
    if style != "" && style != "normally"
        JMap.setStr(setting_id, "style", style)
    endif
    String method = JMap.getStr(obj, "_method", "")
    if method != ""
        JMap.setStr(setting_id, "method", method)
    endif
    String hook = JMap.getStr(obj, "_event_hook", "")
    if hook != ""
        JMap.setStr(setting_id, "event_hook", hook)
    endif
    String tags = JMap.getStr(obj, "_tags", "")
    if tags != ""
        JMap.setStr(setting_id, "tags", tags)
    endif
    String suppress = JMap.getStr(obj, "_tags_suppress", "")
    if suppress != ""
        JMap.setStr(setting_id, "tags_suppress", suppress)
    endif
    int pos_arr = JMap.getObj(obj, "_positions")
    int n = 0
    if pos_arr != 0
        n = JArray.count(pos_arr)
    endif
    if n > 0
        int no_strip_arr = JArray.objectWithSize(n)
        int no_org_arr = JArray.objectWithSize(n)
        int speak_arr = JArray.objectWithSize(n)
        int victim_arr = JArray.objectWithSize(n)
        int i = 0
        while i < n
            int po = JArray.getObj(pos_arr, i)
            int dressed = 0
            int no_org = 0
            String speaking = ""
            int victim = 0
            if po != 0
                dressed = JMap.getInt(po, "_dressed", 0)
                no_org = JMap.getInt(po, "_no_orgasm", 0)
                speaking = JMap.getStr(po, "_speaking", "")
                victim = JMap.getInt(po, "_victim", 0)
            endif
            JArray.setInt(no_strip_arr, i, dressed)
            JArray.setInt(no_org_arr, i, no_org)
            JArray.setStr(speak_arr, i, speaking)
            JArray.setInt(victim_arr, i, victim)
            i += 1
        endwhile
        JMap.setObj(setting_id, "no_stripping", no_strip_arr)
        JMap.setObj(setting_id, "no_orgasm", no_org_arr)
        JMap.setObj(setting_id, "speaking_modifiers", speak_arr)
        JMap.setObj(setting_id, "victim", victim_arr)
    endif
    String filename = GetSceneSettingFilename(setting_name)
    JValue.writeToFile(setting_id, filename)
    JValue.release(setting_id)
    JValue.release(obj)
    Trace("SaveSceneSettingFromWebUIJson", "wrote "+filename)
EndFunction

Function WebUI_OnSceneCreatorSave(int creator_sid, String json)
    SkyrimNet_SexLab_Scene_Creator creator = GetCreatorBySid(creator_sid)
    if creator
        creator.SavePresetFromWebUI(json)
        return
    endif
    SaveSceneSettingFromWebUIJson(json)
EndFunction

Function WebUI_OnAnimationMenuClose(int scene_sid, String json)
    SkyrimNet_SexLab_Scene sl_scene = GetSceneBySid(scene_sid)
    if sl_scene
        sl_scene.WebUI_OnMenuClose(json)
    endif
EndFunction

Function WebUI_OnAnimationMenuPrevNext(int scene_sid, int direction)
    SkyrimNet_SexLab_Scene sl_scene = GetSceneBySid(scene_sid)
    if sl_scene
        sl_scene.WebUI_OnMenuPrevNext(direction)
    endif
EndFunction

Function WebUI_OnAnimationMenuStop(int scene_sid, int direction)
    SkyrimNet_SexLab_Scene sl_scene = GetSceneBySid(scene_sid)
    if sl_scene
        sl_scene.WebUI_OnMenuStop()
    endif
EndFunction

Function WebUI_OnAnimationMenuLiveUpdate(int scene_sid, String json)
    SkyrimNet_SexLab_Scene sl_scene = GetSceneBySid(scene_sid)
    if sl_scene
        sl_scene.WebUI_OnMenuLiveUpdate(json)
    endif
EndFunction

Function WebUI_PushSceneConnections()
    SkyrimNet_SexLab_WebUI.SceneConnections_Show(BuildSceneConnectionsJson())
EndFunction

String Function BuildSceneConnectionsJson()
    int root = JMap.object()
    int arr = JArray.object()
    int neu = JMap.object()
    JMap.setStr(neu, "_id", "new")
    JMap.setStr(neu, "_label", "new")
    JArray.addObj(arr, neu)
    int i = 0
    while i < sl_scenes.length
        SkyrimNet_SexLab_Scene sl_scene = sl_scenes[i]
        if sl_scene != None && sl_scene.GetThreadActive()
            int co = JMap.object()
            JMap.setStr(co, "_id", "scene:"+sl_scene.sid)
            JMap.setInt(co, "_scene_sid", sl_scene.sid)
            JMap.setStr(co, "_label", sl_scene.GetIntentMessage(sl_scene.INTENT_STAGE_ONGOING))
            JArray.addObj(arr, co)
        endif
        i += 1
    endwhile
    JMap.setObj(root, "_connections", arr)
    String json = ObjectToLowerCaseKeyJson(root)
    JValue.release(root)
    return json
EndFunction

String Function BuildAllSceneInfosJson()
    int root = JMap.object()
    int arr = JArray.object()
    SkyrimNet_SexLab_Scene_Creator creator = None
    int i = 0
    while i < creators.length && creator == None
        if creators[i] && creators[i].IsActive()
            creator = creators[i]
        endif
        i += 1
    endwhile
    if creator
        int st = JValue.objectFromPrototype(creator.BuildWebUIState())
        if st
            JArray.addObj(arr, st)
        endif
    else
        int provisional = JMap.object()
        JMap.setStr(provisional, "_mode", "creator")
        JMap.setStr(provisional, "_connection", "new")
        JMap.setInt(provisional, "_creator_sid", 0)
        JMap.setInt(provisional, "_from_target_menu", 0)
        JMap.setObj(provisional, "_positions", JArray.object())
        JArray.addObj(arr, provisional)
    endif
    i = 0
    while i < sl_scenes.length
        SkyrimNet_SexLab_Scene sl_scene = sl_scenes[i]
        if sl_scene != None && sl_scene.GetThreadActive()
            int st = JValue.objectFromPrototype(sl_scene.BuildWebUISceneMenuState())
            if st
                JArray.addObj(arr, st)
            endif
        endif
        i += 1
    endwhile
    JMap.setObj(root, "_scenes", arr)
    String json = ObjectToLowerCaseKeyJson(root)
    JValue.release(root)
    return json
EndFunction

Function WebUI_OnSceneInfoCommit(String json)
    Trace("WebUI_OnSceneInfoCommit", json)
    int root = JValue.objectFromPrototype(json)
    if root == 0
        return
    endif
    int arr = JMap.getObj(root, "_scenes")
    int n = JArray.count(arr)
    int i = 0
    while i < n
        int info = JArray.getObj(arr, i)
        if info > 0
            if JMap.getInt(info, "_pending_create", 0) == 1
                JMap.setStr(info, "_action", "start")
                String payload = ObjectToLowerCaseKeyJson(info)
                WebUI_OnSceneCreatorResult(JMap.getInt(info, "_creator_sid", 0), payload)
            else
                int scene_sid = JMap.getInt(info, "_scene_sid", -1)
                SkyrimNet_SexLab_Scene sl_scene = GetSceneBySid(scene_sid)
                if sl_scene && sl_scene.GetThreadActive()
                    sl_scene.ApplyWebUICommit(info)
                endif
            endif
        endif
        i += 1
    endwhile
    JValue.release(root)
EndFunction

Function WebUI_OnSceneConnectionsRefresh(String unused)
    WebUI_PushSceneConnections()
EndFunction

Function WebUI_OnSceneConnectionChange(String json)
    Trace("WebUI_OnSceneConnectionChange", json)
    WebUI_PushSceneConnections()
    int obj = JValue.objectFromPrototype(json)
    if obj == 0
        return
    endif
    String conn = JMap.getStr(obj, "_connection", "new")
    if conn == "new" || StringUtil.Find(conn, "scene:") != 0
        SkyrimNet_SexLab_Scene_Creator creator = None
        int i = 0
        while i < creators.length && creator == None
            if creators[i] && creators[i].IsActive()
                creator = creators[i]
            endif
            i += 1
        endwhile
        if creator
            int st = JValue.objectFromPrototype(creator.BuildWebUIState())
            if st
                JMap.setStr(st, "_mode", "creator")
                JMap.setStr(st, "_connection", "new")
                String out = ObjectToLowerCaseKeyJson(st)
                JValue.release(st)
                SkyrimNet_SexLab_WebUI.SceneCreator_Configure(out)
            else
                SkyrimNet_SexLab_WebUI.SceneCreator_Configure(creator.BuildWebUIState())
            endif
        else
            int provisional = JMap.object()
            JMap.setStr(provisional, "_mode", "creator")
            JMap.setStr(provisional, "_connection", "new")
            JMap.setInt(provisional, "_creator_sid", 0)
            JMap.setInt(provisional, "_from_target_menu", 0)
            JMap.setObj(provisional, "_positions", JArray.object())
            String out = ObjectToLowerCaseKeyJson(provisional)
            JValue.release(provisional)
            SkyrimNet_SexLab_WebUI.SceneCreator_Configure(out)
        endif
    else
        String sid_str = StringUtil.Substring(conn, 6)
        int scene_sid = sid_str as int
        SkyrimNet_SexLab_Scene sl_scene = GetSceneBySid(scene_sid)
        if sl_scene == None || !sl_scene.GetThreadActive()
            Trace("WebUI_OnSceneConnectionChange", "no active scene sid:"+scene_sid, true)
            JValue.release(obj)
            return
        endif
        ; Soft configure both; do not showPanel (avoids main_panel thrash).
        SkyrimNet_SexLab_WebUI.SceneCreator_Configure(sl_scene.BuildWebUISceneMenuState())
        SkyrimNet_SexLab_WebUI.Animation_Menu_Configure(sl_scene.BuildWebUIAnimationMenuState())
    endif
    JValue.release(obj)
EndFunction

Function WebUI_OnSceneAnimUpdate(int scene_sid, String json)
    SkyrimNet_SexLab_Scene sl_scene = GetSceneBySid(scene_sid)
    if sl_scene
        sl_scene.WebUI_OnAnimUpdate(json)
    endif
EndFunction

Function WebUI_OnAnimRegistrySave(String json)
    int obj = JValue.objectFromPrototype(json)
    if obj == 0
        return
    endif
    String registry = JMap.getStr(obj, "_registry", "")
    if registry == ""
        JValue.release(obj)
        return
    endif
    int payload = JMap.object()
    if JMap.hasKey(obj, "_stages")
        int stages_arr = JMap.getObj(obj, "_stages")
        int count = JArray.count(stages_arr)
        int i = 0
        while i < count
            int st = JArray.getObj(stages_arr, i)
            if st > 0
                int stage_no = JMap.getInt(st, "_stage", i + 1)
                String template = JMap.getStr(st, "_template", "")
                if template != ""
                    int stage_obj = JMap.object()
                    JMap.setStr(stage_obj, "description", template)
                    JMap.setObj(payload, "stage "+stage_no, stage_obj)
                endif
            endif
            i += 1
        endwhile
    endif
    if JMap.hasKey(obj, "_positions")
        int pos_arr = JMap.getObj(obj, "_positions")
        int count = JArray.count(pos_arr)
        int orgasm_arr = JArray.objectWithSize(count)
        int i = 0
        while i < count
            int po = JArray.getObj(pos_arr, i)
            int no_org = 0
            if po > 0
                no_org = JMap.getInt(po, "_no_orgasm", 0)
            endif
            JArray.setInt(orgasm_arr, i, 1 - no_org)
            i += 1
        endwhile
        JMap.setObj(payload, "orgasm_expected", orgasm_arr)
    endif
    String save_json = ObjectToLowerCaseKeyJson(payload)
    JValue.release(payload)
    animdb.SaveAnimLocal(registry, save_json)
    JValue.release(obj)
EndFunction

; JS onResolveActorMeta → enrich Scene Creator positions with SexLab gender + race key.
Function WebUI_OnResolveActorMeta(String json)
    int req = JValue.objectFromPrototype(json)
    if req == 0
        Trace("WebUI_OnResolveActorMeta", "bad json", true)
        return
    endif
    String request_id = JMap.getStr(req, "_request_id", "")
    int form_ids = JMap.getObj(req, "_form_ids")
    bool owned_form_ids = false
    if form_ids == 0
        form_ids = JArray.object()
        owned_form_ids = true
        int single = JMap.getInt(req, "_form_id", 0)
        if single != 0
            JArray.addInt(form_ids, single)
        endif
    endif
    int actors_arr = JArray.object()
    int i = 0
    int n = JArray.count(form_ids)
    while i < n
        int form_id = JArray.getInt(form_ids, i)
        Actor ak = None
        if form_id != 0
            ak = Game.GetFormEx(form_id) as Actor
        endif
        int po = JMap.object()
        JMap.setInt(po, "_form_id", form_id)
        if ak
            int gender = 0
            if sexlab
                gender = sexlab.GetGender(ak)
            endif
            JMap.setInt(po, "_gender", gender)
            JMap.setStr(po, "_race_key", GetRaceKeyForActor(sexlab, ak))
            JMap.setStr(po, "_name", ak.GetDisplayName())
        else
            JMap.setInt(po, "_gender", 0)
            JMap.setStr(po, "_race_key", "")
        endif
        JArray.addObj(actors_arr, po)
        i += 1
    endwhile
    int out = JMap.object()
    JMap.setStr(out, "_request_id", request_id)
    JMap.setObj(out, "_actors", actors_arr)
    String out_json = ObjectToLowerCaseKeyJson(out)
    JValue.release(req)
    JValue.release(out)
    if owned_form_ids
        JValue.release(form_ids)
    endif
    SkyrimNet_SexLab_WebUI.ActorAnimMeta_Result(out_json)
EndFunction
   
;----------------------------------------------------------------------------------------------------
; Action Events
;----------------------------------------------------------------------------------------------------
Function RegisterEventsActions() 
    Trace("RegisterEventsActions","")
    UnRegisterForModEvent("SkyrimNet_SexLab_Action_Stop")
    UnRegisterForModEvent("SkyrimNet_SexLab_Action_Start")
    RegisterForModEvent("SkyrimNet_SexLab_Action_Stop", "Action_Stop")
    RegisterForModEvent("SkyrimNet_SexLab_Action_Start", "Action_Start")
EndFunction 

Event Action_Stop(Form f_speaker,Form f_target, String style)
    Actor speaker = f_speaker as Actor 
    Actor target = f_target as Actor 
    if speaker == None 
        Trace("Action_Stop", "f_speaker is none, aborting")
        return 
    endif 
    if f_target == None 
        Trace("Action_Stop", "f_target is none, aborting")
        return 
    endif 
    if target == None 
        Trace("Action_Stop", "target is none, aborting")
        return 
    endif 

    Trace("Action_Stop", "speaker: "+speaker.GetDisplayName()+" target: "+target.GetDisplayName()+" style: "+style)
    SkyrimNet_SexLab_Scene sl_scene = GetSceneByActor(target)
    if sl_scene == None 
        Trace("Action_Stop", "No sl_scene found for target: "+target.GetDisplayName())
        return 
    endif 
    if main == None 
        Trace("Action_Stop", "main is None")
        return  
    endif 

    Actor Player = Game.GetPlayer() 
    if sl_scene.has_player
        if speaker != player && SkyrimNetApi.GetConfigBool("Plugin_SkyrimNet_SexLab", "sexlab.tagEdit.playerDialogs", true)
            int yes = 0
            int no = 1
            int no_forcefully = 2
            int no_gently = 3
            int no_silently = 4
            String[] buttons = new String[5]
            buttons[yes] = "Yes"
            buttons[no] = "No"
            buttons[no_forcefully] = "No (forcefully)"
            buttons[no_gently] = "No (gently)"
            buttons[no_silently] = "No (silently)"
            String intent
            String question = speaker.GetDisplayName()+" is trying to stop "+sl_scene.GetIntentMessage(sl_scene.INTENT_STAGE_ONGOING)+", will you allow it?"
            int button = SkyMessage.showArray(question, buttons, getIndex = True) as int 
            if button != yes
                if button == no_silently
                    return 
                endif 
                String player_style = "" 
                if button == no_forcefully
                    player_style = "forcefully"
                elseif button == no_gently
                    player_style = "gently"
                endif 
                String msg = player.GetDisplayName()+" "+player_style+" refuses "+speaker.GetDisplayName()+"'s attempt to "+style+" stop "\
                    +sl_scene.GetIntentMessage(sl_scene.INTENT_STAGE_ONGOING)+"."
                DirectNarration(msg, speaker)
                return
            endif 
        endif 
    endif 

    sslThreadController cachedThread = sl_scene.GetThread()
    sl_scene.AnimationEnd(speaker,style)
    threadSlots.StopThread(cachedThread)
EndEvent 

Event Action_Start(String intent, Form f_speaker, Form f_target, Form f_victim, \
    string style, string tags, int speaker_position,\ 
    String event_hook, String setting_name,\ 
    Form f_participate_3)
    Trace("Action_Start","intent:"+intent)
    Actor speaker = f_speaker as Actor 
    Actor target = f_target as Actor 
    Actor victim = f_victim as Actor 
    Actor participate_3 = f_participate_3 as Actor 

    Trace("Action_Start","intent:"+intent\
        +" speaker:"+GetDisplayName(speaker)+" target:"+GetDisplayName(target)+" victim:"+GetDisplayName(Victim)\
        +" style:"+style+" tags:"+tags+" speaker_position:"+speaker_position+" event_hook:"+event_hook+" setting_name:"+setting_name\
        +" participate_3:"+GetDisplayName(participate_3))

    if speaker == None 
        Trace("StartScene_Event", "speaker is None")
        return 
    endif 

    ; ----------------------------
    ; Build the actors array
    ; ----------------------------
    int num_actors = 1 
    if target != None 
        num_actors += 1 
        if participate_3 != None 
            num_actors += 1 
        endif 
    endif 
    Actor[] actors = PapyrusUtil.ActorArray(num_actors)
    if target == None 
        actors[0] = speaker
    else
        if speaker_position == 0 
            actors[0] = speaker 
            actors[1] = target 
        else 
            actors[1] = speaker 
            actors[0] = target 
        endif 
        if participate_3 != None 
            actors[2] = participate_3
        endif 
    endif 

    SkyrimNet_SexLab_Scene_Creator creator = CreateCreator(intent, actors, speaker, target, tags, setting_name)
    ; TargetMenu Start: consume one-shot even if CreateCreator fails (avoid leak to next scene).
    Bool skipSceneCreator = SkyrimNet_SexLab_WebUI.ConsumeSkipSceneCreator()
    if creator == None 
        Trace("Action_Start", "CreateCreator returned None, aborting")
        return 
    endif 
    ; User chose Start (not Custom) — do not open Scene Creator for this scene.
    if skipSceneCreator
        creator.scene_creator_menu_called = true
        Trace("Action_Start", "SkipSceneCreatorOnce consumed for sid:"+creator.sid)
    endif 
    if creator.LockAllActorLock()
        ; Can't be set by setting
        if victim != None 
            creator.SetVictim(victim)
        endif 
        if style != ""
            creator.SetStyle(style) 
        endif 
        ; Can overwrite the setting values 
        if event_hook != "" 
            creator.SetEventHook(event_hook) 
        endif 

        Creator.StartScene() 
    else 
        creator.Release()
    endif 
EndEvent 


;----------------------------------------------------------------------------------------------------
; SexLab Events
;----------------------------------------------------------------------------------------------------
Function RegisterEventsSexlab() 
    Trace("RegisterSexlabEvents","")
    ; SexLabFramework sexlab = Game.GetForm

    UnRegisterForModEvent("HookAnimationStart")
    RegisterForModEvent("HookAnimationStart", "AnimationStart")
    UnRegisterForModEvent("HookStageStart")
    RegisterForModEvent("HookStageStart", "StageStart")
    ;UnRegisterForModEvent("HookStageEnd")
    ;RegisterForModEvent("HookStageEnd", "SexLab_StageEnd")
    UnRegisterForModEvent("HookAnimationEnd")
    RegisterForModEvent("HookAnimationEnd", "AnimationEnd")

    UnRegisterForModEvent("HookOrgasmStart")
    UnRegisterForModEvent("SexLabOrgasm")
    RegisterForModEvent("SexLabOrgasm", "OrgasmIndividual")
    UnRegisterForModEvent("HookOrgasmStart")
    RegisterForModEvent("HookOrgasmStart", "OrgasmCombined")
EndFunction 

; ----------------------------------------------------------
Event AnimationStart(int ThreadID, bool HasPlayer)
    if sexlab == None
        Trace("AnimationStart","Sexlab is None, aborting")
        return
    endif
    sslThreadController thread = SexLab.GetController(ThreadID)
    if thread == None
        Trace("AnimationStart","Thread is None for ThreadID "+ThreadID)
        return
    endif
    SkyrimNet_SexLab_Scene sl_scene = EnsureSceneForThread(thread)
    if sl_scene == None 
        Trace("AnimationStart","Scene is None for ThreadID "+ThreadID)
        return
    endif
    sl_scene.AnimationStart() 
EndEvent 


; ----------------------------------------------------------
Event StageStart(int ThreadID, bool HasPlayer)
    if sexlab == None
        Trace("StageStart","Sexlab is None, aborting")
        return
    endif
    sslThreadController thread = SexLab.GetController(ThreadID)
    if thread == None
        Trace("StageStart","Thread is None for ThreadID "+ThreadID)
        return
    endif
    SkyrimNet_SexLab_Scene sl_scene = EnsureSceneForThread(thread)
    if sl_scene == None 
        Trace("StageStart","Scene is None for ThreadID "+ThreadID)
        return
    endif
    sl_scene.StageStart() 
EndEvent


; ----------------------------------------------------------
event AnimationEnd(int ThreadID, bool HasPlayer)
    ; create_if_missing=False: Action_Stop may already have AnimationEnd+Release; do not allocate a new scene.
    SkyrimNet_SexLab_Scene sl_scene = GetSceneByThreadId(ThreadID, any_state=True, create_if_missing=False)
    if sl_scene == None 
        Trace("AnimationEnd","Scene is None for ThreadID "+ThreadID)
    else 
        sslThreadController[] threads = ThreadSlots.Threads
        int i = threads.length - 1 
        bool found = false
        while 0 <= i && !found
            String s = (threads[i] as sslThreadModel).GetState()
            if s == "animating" || s == "prepare"
                found = true
            endif 
            i -= 1
        endwhile
        if found
            main.active_sex = true
        else 
            main.active_sex = false
        endif
        sl_scene.AnimationEnd() 
    endif
EndEvent 

; Function AllowedDeniedOnlyIncrease(Actor[] actors, sslThreadController thread, String status)
    ; if !Game.GetModByName("SexLabAroused.esm")  != 255
        ; return
    ; endif
    ; Store orgasm denied actor's arousal level before sex, It is not allowed to lower 
    ;q = Game.GetFormFromFile(0x800, "SkyrimNet_SexLab.esp") as Quest
    ;SkyrimNet_SexLab_main main = q as SkyrimNet_SexLab_Main
    ;SkyrimNet_SexLab_Stages stages_lib = q as SkyrimNet_SexLab_Stages

    ; int[] orgasm_denied = new int [1] ; stages.GetOrgasmDenied(thread)
    ; int satisifcation_idx = slaInternalModules.RegisterStaticEffect("Orgasm")
; 
    ; int i = orgasm_denied.length - 1
    ; while 0 <= i    
        ; float sat_value = slaInternalModules.GetStaticEffectValue(actors[i], satisifcation_idx)
        ; if orgasm_denied[i] == 1
            ; if status == "start"
                ; StorageUtil.SetFloatValue(actors[i], storage_arousal_key, sat_value)
            ; else
                ; float stored_value = StorageUtil.GetFloatValue(actors[i], storage_arousal_key)
                ; if stored_value < sat_value
                    ; StorageUtil.SetFloatValue(actors[i], storage_arousal_key, sat_value)
                ; elseif stored_value > sat_value
                    ; slaInternalModules.SetStaticArousalValue(actors[i], satisifcation_idx, stored_value)
                    ; Trace("AllowedDeniedOnlyIncrease",actors[i].GetDisplayName()+" orgasm denied, so erasing orgasm satisifaction "+sat_value+" -> "+stored_value)
                ; endif 
            ; endif 
        ; endif 
        ; sat_value = slaInternalModules.GetStaticEffectValue(actors[i], satisifcation_idx)
        ; i -= 1
    ; endwhile
; EndFunction

; ----------------------------------------------------------------------------------------------------
; Orgasm Event Functions 
; This function is not called when flag SLSO, as it has its own orgasm handling
; ----------------------------------------------------------------------------------------------------
Event OrgasmCombined(int ThreadID, bool HasPlayer)
    ; Ignore if separate orgasms is on, as it has its own handling
    sslSystemConfig config = (SexLab as Quest) as sslSystemConfig
    if config.SeparateOrgasms 
        return 
    endif 
    SkyrimNet_SexLab_Scene sl_scene = GetSceneByThreadId(ThreadID)
    if sl_scene == None 
        Trace("OrgasmCombined","Scene is None for ThreadID "+ThreadID)
        return
    endif
    sl_scene.OrgasmCombined() 
EndEvent 

; Used for SLSO.esp orgasm handling
; SexLab PushForm sends attached-script type (e.g. WIDeadBodyCleanupScript); receive Form then cast.
Event OrgasmIndividual(Form akForm, int full_enjoyment, int num_orgasms)
    Actor akActor = akForm as Actor
    if !akActor
        return
    endif

    sslSystemConfig config = (SexLab as Quest) as sslSystemConfig
    if !config.SeparateOrgasms 
        return 
    endif 

    ; DOM handles it's own orgasms
    if main.handler_dom.IsDOMSlave(akActor)
        return
    endif 

    SkyrimNet_SexLab_Scene sl_scene = GetSceneByActor(akActor)
    if sl_scene == None
        Trace("OrgasmIndividual","Scene is none for actor: "+akActor.GetDisplayName())
        return
    endif
    sl_scene.OrgasmIndividual(akActor, full_enjoyment, num_orgasms) 
EndEvent

int Function GettotalOrgasms(Actor akActor)
    SkyrimNet_SexLab_Scene sl_scene = GetSceneByActor(akActor)
    if sl_scene == None 
        return 0 
    endif 
    return sl_scene.GettotalOrgasms(akActor)
EndFunction

Function OrgasmCustom(Actor akActor, String msg) 
    sslThreadController thread = GetThreadByActor(akActor, true)
    SkyrimNet_SexLab_Scene sl_scene = None
    if thread != None
        sl_scene = GetSceneByThread(thread, true, false)
    endif
    if sl_scene == None
        sl_scene = FindSceneByActorInThreadScene(akActor)
    endif
    if sl_scene == None 
        Trace("OrgasmCustom", "--- scene is None for "+GetDisplayName(akActor)+", aborting")
        return 
    endif 
    Trace("OrgasmCustom", "--- "+GetDisplayName(akActor)+" "+msg)
    sl_scene.OrgasmCustom(akActor, msg + ". "+GetDisplayName(akActor)+" is orgasming.")
EndFunction



; ------------------------------------------------------
; JSON 
; ------------------------------------------------------
Function SaveThreadsJson()
    GetThreadsJson()
EndFunction

String Function GetThreadsJson(Actor speaker = None)
    if speaker == None 
        if speaker_last != None 
            speaker = speaker_last
        else 
            speaker = Game.GetPlayer()
        endif 
    else 
        speaker_last = speaker
    endif 

    if main == None
        Trace("GetthreadsJson","main is None")
        return "{}"
    endif

    sslThreadController[] threads = ThreadSlots.Threads

    int obj = JMap.object() 
    JMap.setStr(obj, "counter", thread_counter)
    thread_counter += 1 

    int threads_array = JArray.object() 
    int i = 0
    while i < threads.length
        ; Read-only: do not allocate/Setup scenes while dumping JSON
        SkyrimNet_SexLab_Scene sl_scene = GetSceneByThread(threads[i], False, False)
        if sl_scene != None 
            if sl_scene.GetThreadActive() 
                JArray.addObj(threads_array, sl_scene.GetThreadObj(speaker))
            endif 
        endif 
        i += 1
    endwhile

    int threads_dom = 0
    int dom_count = 0
    int dom_kept = 0
    if main.handler_dom
        threads_dom = main.handler_dom.GetThreads()
    endif
    if threads_dom
        dom_count = JArray.count(threads_dom)
        i = dom_count - 1
        while i >= 0
            int thread = JArray.getObj(threads_dom, i)
            String description = JMap.getStr(thread, "description")
            if description != ""
                ; Enrich actors for prompts without SetActor StorageUtil / orgasm side effects
                int actor_objs = JMap.getObj(thread, "actors")
                int j = JArray.count(actor_objs) - 1
                Actor akActor = None
                bool speaker_in_thread = false
                while j >= 0
                    int actor_obj = JArray.getObj(actor_objs, j)
                    Actor a = JMap.getForm(actor_obj, "form") as Actor
                    if a != None
                        akActor = a
                        if speaker != None && a == speaker
                            speaker_in_thread = true
                        endif
                        EnrichActorObjForJson(actor_obj, a)
                    endif
                    j -= 1
                endwhile

                float distance = 0.0
                bool los = false
                if speaker != None
                    if speaker_in_thread
                        distance = 1.0
                        los = true
                    elseif akActor != None
                        distance = 0.0142875 * speaker.GetDistance(akActor)
                        los = speaker.HasLOS(akActor)
                    endif
                endif

                AddStrIfNotDefined(thread, "location", "floor")
                AddStrIfNotDefined(thread, "style", "normal")
                JMap.setFlt(thread, "speaker_distance", distance)
                JMap.setInt(thread, "speaker_los", los as int)
                JArray.addObj(threads_array, thread)
                dom_kept += 1
            endif
            i -= 1
        endwhile
    endif
    Trace("GetThreadsJson", "--- dom threads:"+dom_count+" kept:"+dom_kept)

    JMap.setObj(obj, "threads", threads_array) 

    String json = SkyrimNet_SexLab_Utilities.ObjectToLowerCaseKeyJson(obj) 
    
    JValue.release(obj) 
    if threads_dom
        JValue.release(threads_dom)
    endif
    Miscutil.WriteToFile(threads_filename, json, append=False)
    return json
EndFunction 

; Prompt-safe actor fields for DOM threads. No StorageUtil / SexLab thread mutation.
Function EnrichActorObjForJson(int actor_obj, Actor akActor)
    if actor_obj == 0 || akActor == None
        return
    endif
    JMap.setStr(actor_obj, "uuid", UuidToDecimalString(SkyrimNetApi.GetEntityUUID(akActor)))
    JMap.setStr(actor_obj, "formid", akActor.GetFormID())
    AddStrIfNotDefined(actor_obj, "name", akActor.GetDisplayName())
    if !JMap.hasKey(actor_obj, "victim")
        JMap.setInt(actor_obj, "victim", 0)
    endif
    if !JMap.hasKey(actor_obj, "arousal")
        JMap.setInt(actor_obj, "arousal", -1)
    endif
    if !JMap.hasKey(actor_obj, "notice_level")
        JMap.setStr(actor_obj, "notice_level", "nothing")
    endif
    if !JMap.hasKey(actor_obj, "creature_description")
        JMap.setStr(actor_obj, "creature_description", "")
    endif
    if !JMap.hasKey(actor_obj, "is_hermaphrodiate")
        JMap.setInt(actor_obj, "is_hermaphrodiate", 0)
    endif
    if !JMap.hasKey(actor_obj, "wearing_strapon")
        JMap.setInt(actor_obj, "wearing_strapon", 0)
    endif
    if !JMap.hasKey(actor_obj, "speaking_modifiers")
        JMap.setObj(actor_obj, "speaking_modifiers", JArray.object())
    endif
    if main != None && main.handler_dom.IsDOMSlave(akActor)
        JMap.setInt(actor_obj, "dom_slave", 1)
    else
        JMap.setInt(actor_obj, "dom_slave", 0)
    endif
EndFunction

Function AddStrIfNotDefined(int obj, String key_, String value)
    if JMap.hasKey(obj, key_)
        return
    endif
    JMap.setStr(obj, key_, value)
EndFunction

String Function GetStyleDialog(String msg) global
    String[] buttons = new String[4]
    buttons[0] = "forcefully"
    buttons[1] = "normally"
    buttons[2] = "gently"
    buttons[3] = "silently"
    return SkyMessage.ShowArray(msg, buttons, getIndex=False) as String
EndFunction

; ----------------------------------------------------------------------------------------------------
; Check if actor is busy
; ----------------------------------------------------------------------------------------------------

bool Function IsBusy(Actor akActor) 
    if akActor == None 
        Trace("IsActorBusy","akActor is None")
        return false
    endif

    if akActor.IsDead() || akActor.IsInCombat() 
        return true 
    endif 

    if sexlab.IsActorActive(akActor) 
        return true 
    endif 

    if OstimActorCountFaction != None && akActor.IsInFaction(OStimActorCountFaction)
        return true 
    endif

    return false
EndFunction